/**
 * JJTree template file created by SF JavaCC plugin 1.5.28+ wizard for JavaCC 1.5.0+
 */
options
{
  STATIC = false;
  MULTI = true;
  VISITOR = true;
  UNICODE_INPUT = false;
}

PARSER_BEGIN(Parser)
package com.ryoryo.compiler.parser;

import java.io.InputStream;
import java.util.ArrayList;
import java.util.List;

import com.ryoryo.compiler.expression.Expression;

public class Parser
{
  public static Expression parse(InputStream stream) {
    Parser parser = new Parser(stream);
    ParserVisitor visitor = new ASTConverter();
    try
    {
      return (Expression) parser.Root().jjtAccept(visitor, null);
    }
    catch (Exception e)
    {
      System.out.println("Oops.");
      System.out.println(e.getMessage());
      return null;
    }
  }
}

PARSER_END(Parser)

SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| < "//" (~[ "\n", "\r" ])*
    (
      "\n"
    | "\r"
    | "\r\n"
    ) >
| < "/*" (~[ "*" ])* "*"
    (
      ~[ "/" ] (~[ "*" ])* "*"
    )*
    "/" >
}

TOKEN : /* LITERALS */
{
  < INTEGER_LITERAL :
    < DECIMAL_LITERAL > ([ "l", "L" ])?
  | < HEX_LITERAL > ([ "l", "L" ])?
  | < OCTAL_LITERAL > ([ "l", "L" ])? 
    >
| < #DECIMAL_LITERAL : [ "1"-"9" ] ([ "0"-"9" ])* >
| < #HEX_LITERAL : "0" [ "x", "X" ] ([ "0"-"9", "a"-"f", "A"-"F" ])+ >
| < #OCTAL_LITERAL : "0" ([ "0"-"7" ])* >
| < BOOLEAN_LITERAL :
    "true"
  | "false" >
}

TOKEN : /* IDENTIFIERS */
{
  < IDENTIFIER :
    < LETTER >
    (
      < LETTER >
    | < DIGIT >
    )* >
| < #LETTER : [ "_", "a"-"z", "A"-"Z" ] >
| < #DIGIT : [ "0"-"9" ] >
}

TOKEN:
{
  < ADD_OP :
    "+"
  | "-" >
| < MUL_OP :
    "*"
  | "/"
  | "%" >
| < AND_OP : "&&" >
| < OR_OP : "||" >
| < COMP_OP :
    "=="
  | "!="
  | "<"
  | "<="
  | ">"
  | ">=" >
}

SimpleNode Root() :
{}
{
  Program() ";"
  {
    return jjtThis;
  }
}

void Program() :
{}
{
  LogicalExpression()
//| ArithmeticExpression()
}

void LogicalExpression() :
{}
{
  OrExpression()
}

void OrExpression() :
{
  List<Token> tokens = new ArrayList<Token>();
  Token t = null;
}
{
  AndExpression()
  (
    t = < OR_OP >
    AndExpression() { tokens.add(t); }
  )*
  {
    jjtThis.jjtSetValue(tokens);
  }
}

void AndExpression() :
{
  List<Token> tokens = new ArrayList<Token>();
  Token t = null;
}
{
  LogicalUnaryExpression()
  (
    t = < AND_OP >
    LogicalUnaryExpression() { tokens.add(t); }
  )*
  {
    jjtThis.jjtSetValue(tokens);
  }
}

void LogicalUnaryExpression() :
{}
{
  "(" LogicalExpression() ")"
| "!" LogicalExpression()
| ComparisonExpression()
| Boolean()
}


void ComparisonExpression() :
{
  Token t = null;
}
{
  ArithmeticExpression()
  t = < COMP_OP >
  ArithmeticExpression()
  {
    jjtThis.jjtSetValue(t);
  }
}

void Boolean() :
{
  Token t = null;
}
{
  t = < BOOLEAN_LITERAL >
  {
    jjtThis.jjtSetValue(t);
  }
}

void ArithmeticExpression() :
{}
{
  AdditiveExpression()
}

void AdditiveExpression() :
{
  List<Token> tokens = new ArrayList<Token>();
  Token t = null;
}
{
  MultiplicativeExpression()
  (
    t = < ADD_OP >
    MultiplicativeExpression() { tokens.add(t); }
  )*
  {
    jjtThis.jjtSetValue(tokens);
  }
}

void MultiplicativeExpression() :
{
  List<Token> tokens = new ArrayList<Token>();
  Token t = null;
}
{
  UnaryExpression()
  (
    t = < MUL_OP >
    UnaryExpression() { tokens.add(t); }
  )*
  {
    jjtThis.jjtSetValue(tokens);
  }
}

void UnaryExpression() :
{}
{
  "(" ArithmeticExpression() ")"
| Identifier()
| Integer()
}

void Identifier() :
{
  Token t = null;
}
{
  t = < IDENTIFIER >
  {
    jjtThis.jjtSetValue(t);
  }
}

void Integer() :
{
  Token t = null;
}
{
  t = < INTEGER_LITERAL >
  {
    jjtThis.jjtSetValue(t);
  }
}
